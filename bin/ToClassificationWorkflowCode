#!/usr/bin/env perl6
use DSL::English::ClassificationWorkflows;

my %*SUB-MAIN-OPTS = :named-anywhere;

#| Translates natural language commands into (machine learning) classification workflow programming code.
multi sub MAIN(Str $command,                   #= A string with one or many commands (separated by ';').
               Str :$target = 'WL-ClCon',      #= Target (programming language with optional library spec.)
               Str :$language = 'English',     #= The natural language to translate from.
               Str :$format = 'automatic'      #= The format of the output, one of 'automatic', 'code', 'hash', or 'raku'.
               ) {

    my Str $formatSpec = $format.lc (elem) <auto automatic whatever> ?? 'code' !! $format.lc;

    say ToClassificationWorkflowCode($command, $target, :$language, format => $formatSpec);
}

#| Both target and command as arguments.
multi sub MAIN(Str $target,                    #= Programming language.
               Str $command,                   #= A string with one or many commands (separated by ';').
               Str :$language = 'English',     #= The natural language to translate from.
               Str :$format = 'automatic',     #= The format of the output, one of 'automatic', 'code', 'hash', or 'raku'.
               ) {
    MAIN($command, :$target, :$language, :$format);
}